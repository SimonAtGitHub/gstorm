apply plugin: 'groovy'

group = 'io.github.kdabir.gstorm'
version = '0.7.1-SNAPSHOT'
description = "Groovy Single Table ORM"

sourceSets {
    main.groovy.srcDir 'src'
    test.groovy.srcDir 'test'
}

repositories {
    jcenter()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.2',
            'org.hsqldb:hsqldb:2.3.2'
    testCompile 'junit:junit:4.10',
            'io.github.kdabir.directree:directree:0.2'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3'
    }
}

apply plugin: 'idea'

idea {
    project {
        ipr.withXml { provider ->
            def node = provider.asNode()

            // Setting git with idea
            def vcsConfig = node.component.find { it.'@name' == 'VcsDirectoryMappings' }
            vcsConfig.mapping[0].'@vcs' = 'Git'
        }
    }
    module {
        excludeDirs += [file(".gradle"), file('gradle')]
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Gstorm',
                'Implementation-Version': version,
                'Built-By': System.getProperty("user.name"),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty("java.version"),
                'Built-Gradle': gradle.gradleVersion
    }
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier 'groovydoc'
    from groovydoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives groovydocJar
}

apply plugin: 'maven-publish'

publishing {
    publications {
        gstormMaven(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact groovydocJar

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST

                    description project.description
                    url "https://github.com/kdabir/gstorm"

                    developers {
                        developer {
                            id 'kdabir'
                            name 'Kunal Dabir'
                        }
                    }

                    scm {
                        url 'https://github.com/kdabir/gstorm'
                        connection 'scm:git://github.com/kdabir/gstorm.git'
                        developerConnection 'scm:git://github.com/kdabir/gstorm.git'
                    }

                    licenses {
                        license {
                            name 'MIT License'
                            url 'http://opensource.org/licenses/MIT'
                            distribution 'repo'
                        }
                    }
                }
            } // withXml
        }
    }
}

apply plugin: 'bintray'

// set bintrayUser & bintrayKey in gradle.properties
bintray {
    user = getPropertyOrUseDefault("bintrayUser", "fake_user")
    key = getPropertyOrUseDefault("bintrayKey", "fake_key")
    publications = ['gstormMaven']

    def projectName = project.name
    def projectDescription = project.description

    pkg {
        repo = 'maven'
        name = projectName          // somehow project.* doesn't work in this closure
        desc = projectDescription
        licenses = ['MIT']
    }
    dryRun = true // whether to run this as dry-run, without deploying
}

String getPropertyOrUseDefault(String propertyName, String defaultValue) {
    hasProperty(propertyName) ? getProperty(propertyName) : defaultValue
}
